@ksx executed ("lib/ui").
@ksx import ("lib/telemetry").

parameter targetAP to 90000.

// ----------------------------------------------------------------------------
// functions
// ----------------------------------------------------------------------------
function get_throttle_for_twr {
  parameter tgt. // target TWR
  parameter ves is ship.

  set maxTWR to tlm_twr_max().

  // if we haven't yet staged through to a thrust producing stage, keep the
  // throttle pinned
  if maxTWR = 0 {
    return 1.0.
  }

  return tgt / maxTWR.
}

function get_steering_for_state {
  parameter vessel is ship.

  set v to vessel:velocity:surface:mag.
  set h to vessel:altitude.

  if v < 50 {
    return heading(90, 90).
  } else if v >= 50 and v < 100 {
    return heading(90, 85).
  } else if v >= 300 and v < 600 {
    return heading(90, 75).
  } else if v >= 600 and v < 1000 {
    return heading(90, 45).
  } else if v >= 1000 and v < 1400 {
    return heading(90, 30).
  } else if v >= 1400 {
    return heading(90, 25).
  }

  // unlock the steering controls
  return 0.
}

function is_clamped_to_ground {
  parameter clampName is "launchClamp".
  parameter vessel is ship.

  return vessel:partsnamedpattern(clampName):length > 0.
}

function get_payload_fairings {
  parameter vessel is ship.

  return vessel:partsnamedpattern("fairing").
}

// ----------------------------------------------------------------------------
// begin script
// ----------------------------------------------------------------------------
// set inital throttle and directional controls
set mySteer to heading(90, 90).
set myThrot to 1.0.

lock steering to mySteer.
lock throttle to myThrot.

// if initial stage is engine held by support structure, immediately stage it
// away, the launch engine should be staged first otherwise the clamps will release
// the ship onto the ground possibly breaking the engines.
if is_clamped_to_ground() {
  notify("Staging through launch clamps...").
  stage. wait until stage:ready. stage.
}

// normal staging logic
when maxthrust = 0 then {
  notify("Staging...").
  stage. wait until stage:ready.
  preserve.
}

// discard payload shell when leaving the atmosphere (if it exists)
set fars to get_payload_fairings().
if fars:length > 0 {
  when ship:altitude > 70000 then {
    notify("Ejecting payload fairings...").

    lock steering to "kill".

    for far in fars {
      far:getmodule("ModuleProceduralFairing"):doevent("deploy").
    }

    lock steering to mySteer.
  }
}

clearscreen.
until ship:apoapsis >= targetAP {
  set potentialSteering to get_steering_for_state(ship).

  print "TWR(cur): " + tlm_twr_cur() at (0, 0).
  print "TWR(max): " + tlm_twr_max() at (0, 1).
  print "tt(AP): " + eta:apoapsis at (0, 2).
  print "tt(PE): " + eta:periapsis at (0, 3).

  if potentialSteering = 0 {
    unlock steering.
  } else {
    set mySteer to potentialSteering.
  }

  // above a certain altitude there is no compelling reason to keep thrust TWR
  // limited, pin it
  if ship:altitude > 26000 {
    set myThrot to 1.0.
  } else {
    set myThrot to get_throttle_for_twr(1.9).
  }
}

notify("Target AP reached, returning control...").
lock throttle to 0. set ship:control:pilotmainthrottle to 0.
